buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
}

apply plugin: "bintray"
apply plugin: "maven-publish"

group = 'com.github.myth-china'
version = '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation gradleApi()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

gradlePlugin {
    plugins {
        modularSettings {
            id = 'modular-settings'
            implementationClass = 'com.myth.plugin.modular.SettingsPlugin'
        }
        modularProject {
            id = 'modular-project'
            implementationClass = 'com.myth.plugin.modular.ProjectPlugin'
        }
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "gradleware"
            name "Team Gradleware"
            email "info@gradleware.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A simple “Hello World” Gradle plugin')
                root.children().last() + pomConfig
            }
        }
    }
}